buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.10"
    }
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.matthewprenger.cursegradle"

def number = 'dev';
if (hasProperty('buildNumber')) {
	number = buildNumber;
}

version = "${minecraft_version}-${mod_version}.${number}"
group = mod_namespace // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_name

println "Creating File Version '${version}' for '${archivesBaseName}'"
	
sourceCompatibility = targetCompatibility = "${java_version}"
compileJava {
    sourceCompatibility = targetCompatibility = "${java_version}"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run" 
    mappings = forge_mappings    
	makeObfSourceJar = false
	
	replace '@VERSION@', project.version
	replace "required-after:forge;", "required-after:forge@(${forge_version},);"
}

repositories {
    maven { // JEI
        name = "JEI repo"
        url "http://dvs1.progwml6.com/files/maven"
    }
	maven {
        name = "storagedrawers"
        url = "https://dl.bintray.com/jaquadro/dev/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
}

dependencies {
    deobfCompile "com.jaquadro.minecraft.storagedrawers:StorageDrawers:${storagedrawer_version}:api"
    compile "com.jaquadro.minecraft.storagedrawers:StorageDrawers:${storagedrawer_version}"
    deobfCompile "com.jaquadro.minecraft.chameleon:Chameleon:${chameleon_version}"

    compileOnly "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
	
	extension = 'jar'
	classifier = 'src'
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

artifacts {
    archives srcJar, devJar
}

if (project.hasProperty('curseforgeApi')) {
	curseforge {
		apiKey = "${curseforgeApi}"
		project {
			id = '293968'
			changelog = "https://github.com/Samtrion/${mod_id}/blob/${minecraft_version}/CHANGELOG.md"
			releaseType = "${curseforgeReleaseType}"
			addGameVersion "${minecraft_version}"
			
			addArtifact devJar
			addArtifact srcJar
			
			relations {
				requiredLibrary 'chameleon'
				requiredLibrary 'storage-drawers'
				optionalLibrary 'jei'
				optionalLibrary 'storage-drawers-extras'
			}
		}
	}
}